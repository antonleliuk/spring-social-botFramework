subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "maven"
    apply plugin: "signing"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = "ua.profitsoft.social"
    version = "0.0.2"

    ext {
        localSonatypeUserName = project.hasProperty("sonatypeUsername") ? project.sonatypeUsername : ""
        localSonatypePassword = project.hasProperty("sonatypePassword") ? project.sonatypePassword :""
        signingEnabled = project.hasProperty("signingEnabled") ? project.signingEnabled : false
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        compile("org.springframework.social:spring-social-core:$springSocialVersion"){
//            transitive = false
        }
        compile("org.springframework.social:spring-social-config:$springSocialVersion"){
//            transitive = false
        }
        compile("org.springframework.social:spring-social-security:$springSocialVersion"){
//            transitive = false
        }
        compile ("org.springframework.security:spring-security-core:$springSecurityVersion")
    }

    task sourceJar(type: Jar) {
        classifier = "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourceJar
    }

    signing {
        required { signingEnabled }
        sign configurations.archives
    }
}

task wrapper(type: Wrapper){
    gradleVersion = "3.3"
}

